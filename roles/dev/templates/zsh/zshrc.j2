# Generated by Ansible for {{ ansible_nodename  }}
################################################################################
# zplug 設定
################################################################################
# if [ ! -f ~/.zplug/init.zsh ]; then
#     echo "Installing zplug..."
#     unsetopt BG_NICE
#     curl -sL --proto-redir -all,https https://raw.githubusercontent.com/zplug/installer/master/installer.zsh| zsh
# fi
source ~/.zplug/init.zsh
zplug "zsh-users/zsh-autosuggestions"
zplug "zsh-users/zsh-history-substring-search"
zplug "willghatch/zsh-cdr"
zplug "zsh-users/zsh-completions"

zplug "plugins/git",   	    from:oh-my-zsh
zplug "plugins/autojump",   from:oh-my-zsh
zplug "modules/prompt",     from:prezto
zplug 'dracula/zsh', as:theme

zplug "zsh-users/zsh-syntax-highlighting", defer:2

################################################################################
# tmux
################################################################################
# ssh した時にウィンドウ名に接続先ホスト名を表示
if [[ -n $TMUX_PANE  ]]; then
    function ssh() {
        tmux rename-window "ssh[${@:-1:1]}]" > /dev/null 2>&1
        command ssh "$@"
        tmux set-window-option automatic-rename "on" > /dev/null 2>&1
    }
fi

################################################################################
# nvm
################################################################################
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"

################################################################################
# zoxide
################################################################################
if (which zoxide >& /dev/null) then
   eval "$(zoxide init zsh)"
fi

################################################################################
# fzf
################################################################################
if [ ! -f ~/.fzf.zsh ]; then
    git clone https://github.com/junegunn/fzf.git ~/.fzf
    ~/.fzf/install --all
fi
source ~/.fzf.zsh

export FZF_DEFAULT_COMMAND='ag --hidden --ignore .git -g ""'
export FZF_DEFAULT_OPTS='--height 40% --reverse --inline-info --bind "ctrl-k:kill-line"'
export FZF_TMUX=1

fd() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

fshow() {
  git log --graph --color=always \
      --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |
  fzf --ansi --no-sort --reverse --tiebreak=index --bind=ctrl-s:toggle-sort --preview "" \
      --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 |
                xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
FZF-EOF"
}

fbr() {
  local branches branch
  branches=$(git branch --all | grep -v HEAD) &&
  branch=$(echo "$branches" |
           fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}

j() {
    if [[ "$#" -ne 0 ]]; then
        cd $(autojump $@)
        return
    fi
    cd "$(autojump -s | sed '/_____/Q; s/^[0-9,.:]*\s*//' |  fzf-tmux --inline-info)"
}

fe() {
  local files
  IFS=$'\n' files=($(fzf-tmux --query="$1" --multi --select-1 --exit-0))
  [[ -n "$files" ]] && ${EDITOR:-vim} "${files[@]}"
}

# customize
fzf-history-widget-custom() {
    fc -R # Re-read the history file
    fzf-history-widget
}
zle     -N   fzf-history-widget-custom
bindkey '^R' fzf-history-widget-custom

################################################################################
# プロンプト
################################################################################
# POWERLEVEL9K_MODE='nerdfont-complete'
# POWERLEVEL9K_MODE='awesome-fontconfig'
# POWERLEVEL9K_MODE='awesome-patched'

zplug "bhilburn/powerlevel9k", use:powerlevel9k.zsh-theme

POWERLEVEL9K_SHOW_CHANGESET=true
POWERLEVEL9K_CHANGESET_HASH_LENGTH=6
POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(context dir dir_writable)
POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(vcs time)
POWERLEVEL9K_PROMPT_ON_NEWLINE=true
POWERLEVEL9K_MULTILINE_FIRST_PROMPT_PREFIX=""
POWERLEVEL9K_MULTILINE_LAST_PROMPT_PREFIX=" %% "
POWERLEVEL9K_SHORTEN_DIR_LENGTH=3
POWERLEVEL9K_DIR_ETC_FOREGROUND="195"
POWERLEVEL9K_DIR_HOME_FOREGROUND="195"
POWERLEVEL9K_DIR_DEFAULT_FOREGROUND="195"
POWERLEVEL9K_DIR_HOME_SUBFOLDER_FOREGROUND="195"
POWERLEVEL9K_SHORTEN_DIR_LENGTH=4
POWERLEVEL9K_SHORTEN_STRATEGY="truncate_to_first_and_last"
ZSH_THEME="powerlevel9k/powerlevel9k"

################################################################################
# zplug ロード
################################################################################
zplug check || zplug install
zplug load

################################################################################
# カラー
################################################################################
setopt prompt_subst

case ${SOLARIZED_THEME:-dark} in
    light) bkg=white;;
    *)     bkg=black;;
esac

case `whoami` in
     root) ucolor=red;;
     *)    ucolor=green;;
esac

ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
ZSH_HIGHLIGHT_STYLES[alias]='none'
ZSH_HIGHLIGHT_STYLES[builtin]='none'
ZSH_HIGHLIGHT_STYLES[function]='none'
ZSH_HIGHLIGHT_STYLES[command]='none'
ZSH_HIGHLIGHT_STYLES[precommand]='none'
ZSH_HIGHLIGHT_STYLES[commandseparator]='none'
ZSH_HIGHLIGHT_STYLES[hashed-command]='none'
ZSH_HIGHLIGHT_STYLES[unknown-token]='fg=198'
ZSH_HIGHLIGHT_STYLES[single-quoted-argument]='fg=215'
ZSH_HIGHLIGHT_STYLES[double-quoted-argument]='fg=215'

################################################################################
# kubernetes
################################################################################
if (which kubectl >& /dev/null) then
   eval "$(kubectl completion zsh)"
fi

################################################################################w
# エイリアス
################################################################################
if (which dircolors >& /dev/null) then	# dircolors
   eval `dircolors ~/.dircolors`
fi

if (which exa >& /dev/null) then	    # ls
    alias ls='exa --color=auto'
elif (which gnuls >& /dev/null) then
    alias ls='gnuls -F --color=auto'
elif (uname | grep Linux >& /dev/null) then
    alias ls='ls -F --color=auto'
else
    alias ls='ls -FG'
fi

if (which colordiff >& /dev/null) then	# diff
    alias diff=colordiff
fi

for editor in emacs vim vi              # editor
do
  if (which $editor >& /dev/null) then
      export EDITOR=$editor
      break
  fi
done

alias rm='nocorrect =rm -i'             # スペル訂正をしない
alias mv='nocorrect =mv -i'
alias cp='nocorrect =cp -i'

alias keychain='keychain --nogui'       # keychain

if [[ ($EMACS_VERSION > 22.9) && ($SUDO_USER != root) ]] then # emacs
   emacs () {
       emacsclient -c "$@" 2>/dev/null || (/usr/bin/emacs --daemon; emacsclient -c "$@")
   }
fi

alias less='less -R'                    # less
alias vi='vim'                          # vi

alias ack='ack -group --color'          # ack
alias nl='nl -ba'                       # nl
alias df='df -h'                        # df

alias bell='echo -ne "\a"'              # bell

if [[ -n $TMUX_PANE  ]]; then           # reset
    function reset() {
        echo -ne "\eP\ec\e\\"
        =reset
        clear
        tmux clear-history > /dev/null 2>&1
        tmux set-window-option automatic-rename "on"
    }
else
    function reset() {
        echo -ne "\eP\ec\e\\"
        =reset
        clear
    }
fi

alias reset='echo -ne "\eP\ec\e\\" && =reset && clear && tmux clear-history > /dev/null 2>&1 && tmux set-window-option automatic-rename "on" > /dev/null 2>&1' # rebbset
alias lshw='lshw -disable scsi'         # lshw

if (which duf >& /dev/null) then	    # df
    alias df=duf
fi

################################################################################
# グローバルエイリアス
################################################################################
alias -g L='|& less -R'
alias -g G='|& grep'
alias -g H='| head'
alias -g C='| colordiff'
alias -g T='| tail'
alias -g S='| sort'
alias -g A='| awk'
alias -g W='| wc'

alias -g O='2>&1'
alias -g N='>/dev/null'
alias -g NE='2>/dev/null'
alias -g NB='>&/dev/null'

alias -g P='| perl -pe "s/\e\[\d+(;\d+)?m//g" | col -bx'

################################################################################
# サフィックスエイリアス
################################################################################
if is-at-least 4.2; then
    alias -s {tar.gz,tar.Z,tgz,tar,gz,zip,tar,bz2,tbz2,bz2,lzh,rar,Z,xz}='muncompress'
fi

muncompress () {
    for file in "$@"
      do
      case $file in
          *.tar.gz ) tar xzvf   $file ;;
          *.tar.Z  ) tar xzvf   $file ;;
          *.tgz    ) tar xzvf   $file ;;
          *.tar    ) tar xvf    $file ;;
          *.gz     ) gunzip     $file ;;
          *.zip    ) unzip      $file ;;
          *.tar.bz2) tar xjvf   $file ;;
          *.tbz2   ) tar xjvf   $file ;;
          *.bz2    ) bzip2 -d   $file ;;
          *.lzh    ) lha x      $file ;;
          *.rar    ) rar x      $file ;;
          *.Z      ) uncompress $file ;;
          *.xz     ) xz -d       $file ;;
	      *)
              echo "Can't decide how to uncompress $file."
              ;;
		esac
	done
}

################################################################################
# Keychain 等
################################################################################
stty stop undef
if [[ ($TERM == xterm*) || (-z $SSH_CLIENT && -z $EMACS) ]]; then
    if [[ -z $TMUX_PANE ]]; then
        # keychain
        if [[ -n ~/.ssh/*.id_*(#qN) ]]; then
            if (which =keychain >& /dev/null) then
               keychain ~/.ssh/*.id_*
               source ~/.keychain/`hostname`-sh >& /dev/null
            fi
        fi

        # emacs
        local EMACS_VERSION=${${${(f)"$(emacs --version)"}[1]}##GNU Emacs }
        if [[ $EMACS_VERSION > 22.9 ]] then
            =emacs --daemon >& /dev/null
        fi
        # tmux
        tmux a -d || tmux || echo 'Unable to start tmux.'
    else
        # keychain
        if [[ -e ~/.keychain/`hostname`-sh ]]; then
            source ~/.keychain/`hostname`-sh >& /dev/null
        fi
        clear
    fi
fi

# Local Variables:
# mode: sh
# End:
###############################################################################
